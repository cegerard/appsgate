PG = SET
{
  return "ok";
}

SET = ID "setOfRules" _ "[" _ RULES _ "]"

SEQ = ID "seqRules" _ "[" _ INSTS _ "]"

BLOC = SEQ

INSTS  = _ (INST _)* STOP _ EMPTY /  _ (INST _)+ / "(" INSTS ")"

STOP = ID "stopMyself" _ "(" _ TARGET _ ")"

INST  = IF / ACTION / EMPTY / WAIT / SEQ / WHEN_IMP

RULES  = _ (RULE _)+ / "(" RULES ")" 

RULE = WHEN / WHILE / SET / SEQ / EMPTY

IF  = ID "if" _ "(" BOOLEAN ")" _ "(" _ BLOC _ ")" _ "(" _ BLOC _ ")"
    / ID "if" _ "(" BOOLEAN ")" _ "(" _ BLOC _ ")"

WHEN  = ID "when" _ "(" _ EVT _ ")" _ "(" _ BLOC _")"

WHEN_IMP  = ID "whenImp" _ "(" _ EVT _ ")" _ "(" _ BLOC _")"

WHILE  = ID "while" _ "(" _ STATE _ ")" _ "(" _ KEEP _ ")" _ "(" _ BLOC _ ")"

BLOC_KEEP = ID "seqRules" _ "[" _ (KEEP _)+ "]"

EVENTS  = ID "eventsSequence" _ "[" _ EVENTLIST _ "]"
        / ID "eventsAnd" _ "[" _ EVENTLIST _ "]"
        / ID "eventsOr" _ "[" _ EVENTLIST _ "]"
        / EVT

EVENTLIST = (EVENTS _)+

EVT  = ID "event" _ "(" _ SOURCE _")" _ "("_ PARAM _ ")" / ID "event" _ "(" _ SOURCE _ ")" / ID "eventProgram" _ "(" _ SOURCE _ ")"

PARAM = i:ID "param"_"(" _ DEVICE_SEP l:NUMBERS DEVICE_SEP _ "," _ p:string _ ")"
{
ex = {'message' : 'expected param of type: ' + l.join(","), 'expected':l, 'id' : i, 'type':'param', 'param' : p};
throw ex;
}
 / i:ID "param"

WAIT = ID "wait" _ "(" _ NUMERIC_VALUE _ ")"

/**
 * BOOLEAN expression can manipulate number also, which is not "logical".
 * HACK
 
BOOLEAN_OLD  = ID "booleanExpression" _"(" _ BOOLEAN _ ")" _ "(" _ BOOLEAN _ ")"
         / ID "comparator" _"(" _ NUMERIC_VALUE _ ")" _ "(" _ NUMERIC_VALUE _ ")"
         / ID "booleanExpression" _"(" _ BOOLEAN _ ")"
         / STATE
*/
BOOLEAN  =  ID "comparator" _"(" _ NUMERIC_VALUE _ ")" _ "(" _ NUMERIC_VALUE _ ")"
         / STATE

NUMERIC_VALUE = ID "number"
			/ ID "boolean"
            / PROPERTY
            / ID "scale"
			
ARG = "String" / "number" / "boolean"

ACTION = ACTION_0 / ACTION_1 / ACTION_2 / ACTION_3 / ACTION_WITH_ANY_ARGS
ACTION_WITH_ANY_ARGS = ID "action" _ "(" _ TARGET _ ")" _ "[" _ (ARG _)* _ "]"
ACTION_0 = ID "action0" _ "(" _ TARGET _ ")" _ "[" _ "]"
ACTION_1 = ID "action1" _ "(" _ TARGET _ ")" _ "[" _ ARG _ "]"
ACTION_2 = ID "action2" _ "(" _ TARGET _ ")" _ "[" _ ARG _ ARG _ "]"
ACTION_3 = ID "action3" _ "(" _ TARGET _ ")" _ "[" _ ARG _ ARG _ ARG _ "]"

PROPERTY = ID "property" _ "(" _ SOURCE _ ")"  _ "[" _ (ARG _)* _ "]"

KEEP  = ID "keepState" _ "(" _ MAINTAINABLE_STATE _ ")"

STATE = ID "state" _ "(" _ SOURCE _")" _ "("_ PARAM _ ")" / ID "state" _ "(" _ SOURCE _")" / ID "stateProgram" _ "(" _ SOURCE _ ")"

MAINTAINABLE_STATE = ID "maintainableState" "(" _ TARGET _ ")" / ID "keepStateProgram" _ "(" _ SOURCE _ ")"

TARGET = ID "variable" / ID "select" / ID "device" / ID "service" / ID "programCall" / EMPTY / MANDATORY_SEL

SOURCE = ID "device" / ID "service" / ID "programCall" /EMPTY / MANDATORY

EMPTY = ID "empty"

VALUE = ID "string" / ID "number" / ID "boolean"

ID "ID" = d:[0-9]+":"
{
 return d.join("");
}

MANDATORY_SEL =  i:ID DEVICE_SEP l:NUMBERS DEVICE_SEP
{
ex = {'message' : 'expected device of type: ' + l.join(","), 'expected':l, 'id' : i, 'type':'select'};
throw ex;
}
		/ MANDATORY

MANDATORY = i:ID DEVICE_SEP l:NUMBERS DEVICE_SEP
{
ex = {'message' : 'expected device of type: ' + l.join(","), 'expected':l, 'id' : i, 'type':'device'};
throw ex;
}
         / i:ID "programs"
{
ex = {'message' : "missing a program", 'expected':["programs"], 'id' : i};
throw ex;
}
         / i:ID SERVICE_SEP l:NUMBERS SERVICE_SEP
{
ex = {'message' : 'expected device of type: ' + l.join(","), 'expected':l, 'id' : i, 'type':'service'};
throw ex;
}

NUMBERS = d:(number)+
{
return d;
}

DEVICE_SEP "separator" = "/"
SERVICE_SEP "separator" = "|"

/* ===== Whitespace ===== */

_ "whitespace"
  = whitespace*

// Whitespace is undefined in the original JSON grammar, so I assume a simple
// conventional definition consistent with ECMA-262, 5th ed.
whitespace
  = [ \t\n\r]

number = int_:int _
{
  return parseInt(int_);
}

int
  = digit19:digit19 digits:digits     { return digit19 + digits;       }
  / digit:digit



digits
  = digits:digit+ { return digits.join(""); }


digit
  = [0-9]

digit19
  = [1-9]
  
string "string"
  = "'" "'" _             { return "";    }
  / "'" chars:chars "'" _ { return chars; }

chars
  = chars:char+ { return chars.join(""); }

char
  // In the original JSON grammar: "any-Unicode-character-except-"-or-\-or-control-character"
  = [A-Z]
  / [a-z]
